Функция ПолучитьСКД() Экспорт

	Возврат  ПолучитьМакет("СхемаКомпоновкиДанных");

КонецФункции // ПолучитьСКД()

Функция ПолучитьСправочникиПоОтбору(Настройки) Экспорт

	СКД = ПолучитьСКД();
	
	ДанныеРасшифровки 			= Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета 			= Новый КомпоновщикМакетаКомпоновкиДанных;
	
	//Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки   			= КомпоновщикМакета.Выполнить(СКД, Настройки, ДанныеРасшифровки,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновкиДанных 	= Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки,, ДанныеРасшифровки);
	
	ПроцессорВывода 			= Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ДеревоЗначений = Новый ДеревоЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
	ЭлементыСправочников = Новый Структура;
	ЭлементыСправочников.Вставить("Контрагенты");
	ЭлементыСправочников.Вставить("Номенклатура");
	
	Для Каждого СтрокаДерева Из ДеревоЗначений.Строки Цикл
		ЭлементыСправочников.Вставить(СтрокаДерева.ВидСправочника, СтрокаДерева.Строки.ВыгрузитьКолонку("Ссылка"));
	КонецЦикла;
	
	Возврат ЭлементыСправочников;

КонецФункции // ПолучитьНоменклатуруПоОтбору()

Процедура НастроитьПолеТабличногоДокумента(ТабличныйДокумент) Экспорт
	
	Макет = ПолучитьМакет("МакетТабличногоДокумента");
	
	Область = Макет.ПолучитьОбласть("Строка1|Колонка1");
	ТабличныйДокумент.Вывести(Область);
	
	КоличествоКолонок 	= 100;
	КоличествоСтрок 	= 100;
	
	НомерПервойКолонки 		= Область.ШиринаТаблицы + 1;
	НомерПоследнейКолонки 	= Область.ШиринаТаблицы + КоличествоКолонок;
	НомерПервойСтроки 		= Область.ВысотаТаблицы + 1;
	НомерПоследнейСтроки 	= Область.ВысотаТаблицы + КоличествоСтрок;
	НомерСтрокиИтого 		= 2;
	
	Область = Макет.ПолучитьОбласть("Строка1|Колонка2");
	
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		ТабличныйДокумент.Присоединить(Область);
	КонецЦикла;
	
	ОбластьСтрока = Новый ТабличныйДокумент;
	ОбластьСтрока.Вывести(Макет.ПолучитьОбласть("Строка2|Колонка1"));
	Область = Макет.ПолучитьОбласть("Строка2|Колонка2");
	
	Для НомерКолонки = 1 По КоличествоКолонок Цикл
		ОбластьСтрока.Присоединить(Область);
	КонецЦикла;
	
	Для НомерСтроки = 1 По КоличествоСтрок Цикл
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ТабличныйДокумент.ФиксацияСверху = 2;
	ТабличныйДокумент.ФиксацияСлева = 1;

КонецПроцедуры

Процедура ЗагрузитьНастройкиВКомпоновщикНастроек(УИ) Экспорт
//Процедура ЗагрузитьНастройкиВКомпоновщикНастроек(КомпоновщикНастроек) Экспорт
	
	СКД = ПолучитьСКД();
	АдресСКД = ПоместитьВоВременноеХранилище(СКД, УИ);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД));
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);

КонецПроцедуры

#Область СозданиеДокументов

Функция СоздатьДокументы(ТабДокумент, ПараметрыСозданияДокументов = Неопределено) Экспорт
	
	ВидыДокументов = Новый Структура("ОПЗС,РТУ", "ОтчетПроизводстваЗаСмену", "РеализацияТоваровУслуг");
	
	ПараметрыЗаполнения 	= ПолучитьДанныеЗаполнения();
	ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ПараметрыСозданияДокументов);
	
	Если Не (ПараметрыЗаполнения.СоздаватьОтчетыПроизводства Или ПараметрыЗаполнения.СоздаватьРеализации) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеОТоварах = ПолучитьДанныеОТоварах(ТабДокумент);
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(ДанныеОТоварах.Номенклатура, ПараметрыЗаполнения, Ложь, Истина);
	
	НужныСпецификации 	= ПараметрыЗаполнения.СоздаватьОтчетыПроизводства;
	Спецификации 		= ?(НужныСпецификации, ПолучитьСпецификации(ДанныеОТоварах.Номенклатура), Новый Соответствие);
	
	НужныДоговоры 	= ПараметрыЗаполнения.СоздаватьРеализации;
	Договоры 		= ?(НужныДоговоры, ПолучитьДоговорыСРеквизитами(ДанныеОТоварах.Контрагенты, Организация), Неопределено);
	
	#Область ТаблицаДокументов
	
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("Документ");
	ТаблицаДокументов.Колонки.Добавить("ТаблицаТоваров");
	ТаблицаДокументов.Колонки.Добавить("ИмяТаблЧасти");
	
	// ОтчетПроизводстваЗаСмену
	Если ПараметрыЗаполнения.СоздаватьОтчетыПроизводства Тогда
		
		ТаблицаТоваров = ПолучитьТаблицуИзДереваТоваров(ДанныеОТоварах.ДеревоНоменклатуры);
		ТаблицаТоваров.Свернуть("Номенклатура", "Количество");
		
		НовыйДокумент 	= СоздатьДокументОбъект(ВидыДокументов.ОПЗС, ПараметрыЗаполнения);
		НоваяСтрока 	= ТаблицаДокументов.Добавить();
		НоваяСтрока.Документ 		= НовыйДокумент;
		НоваяСтрока.ТаблицаТоваров 	= ТаблицаТоваров;
		НоваяСтрока.ИмяТаблЧасти 	= "Продукция";
		
	КонецЕсли;
	
	// РеализацияТоваровУслуг
	Если ПараметрыЗаполнения.СоздаватьРеализации Тогда
		
		Для Каждого СтрокаПолучатель Из ДанныеОТоварах.ДеревоНоменклатуры.Строки Цикл
			
			Если Не ЗначениеЗаполнено(СтрокаПолучатель.Контрагент) Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыЗаполненияШапки = Новый Структура;
			ПараметрыЗаполненияШапки.Вставить("Контрагент", 			СтрокаПолучатель.Контрагент);
			ПараметрыЗаполненияШапки.Вставить("ДоговорКонтрагента", 	ПолучитьДоговор(Договоры, СтрокаПолучатель.Контрагент));
			
			НовыйДокумент = СоздатьДокументОбъект(ВидыДокументов.РТУ, ПараметрыЗаполнения, ПараметрыЗаполненияШапки);
			НовыйДокумент.ДополнительныеСвойства.Вставить("ЗаполнитьСчетаУчетаПередЗаписью", Истина);
			НоваяСтрока = ТаблицаДокументов.Добавить();
			НоваяСтрока.Документ 		= НовыйДокумент;
			НоваяСтрока.ТаблицаТоваров 	= СтрокаПолучатель.Строки;
			НоваяСтрока.ИмяТаблЧасти 	= "Товары";
			
		КонецЦикла;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ЗаполнениеДокументов
	
	НачатьТранзакцию();
	Попытка
		
		// Заполняем табличную часть документов
		Для Каждого СтрокаДокумент Из ТаблицаДокументов Цикл
			
			Для Каждого СтрокаТовары Из СтрокаДокумент.ТаблицаТоваров Цикл
				
				ПараметрыЗаполненияСтроки = Новый Структура(
				"Количество,Цена,ПлановаяСтоимость,Коэффициент,
				|Номенклатура,ЕдиницаИзмерения,НоменклатурнаяГруппа,Субконто,СтавкаНДС,Спецификация", 0,0,0,1);
				
				ЗаполнитьЗначенияСвойств(ПараметрыЗаполненияСтроки, СтрокаТовары);
				
				Если СведенияОНоменклатуре[СтрокаТовары.Номенклатура] <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(ПараметрыЗаполненияСтроки, СведенияОНоменклатуре[СтрокаТовары.Номенклатура]);
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(ПараметрыЗаполненияСтроки.НоменклатурнаяГруппа) Тогда
					ПараметрыЗаполненияСтроки.НоменклатурнаяГруппа = НоменклатурнаяГруппа;
				КонецЕсли;
				
				Если ПараметрыЗаполненияСтроки.ПлановаяСтоимость = 0 Тогда
					ПараметрыЗаполненияСтроки.ПлановаяСтоимость = 0.01;
				КонецЕсли;
				
				ПараметрыЗаполненияСтроки.Спецификация 	= Спецификации[СтрокаТовары.Номенклатура];
				
				Объект = СтрокаДокумент.Документ;
				
				НоваяСтрока = Объект[СтрокаДокумент.ИмяТаблЧасти].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПараметрыЗаполненияСтроки);
				
				Если ТипЗнч(Объект) = Тип("ДокументОбъект."+ВидыДокументов.РТУ) Тогда
					
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(НоваяСтрока);
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(НоваяСтрока, Объект.СуммаВключаетНДС);
					
				ИначеЕсли ТипЗнч(Объект) = Тип("ДокументОбъект."+ВидыДокументов.ОПЗС) Тогда
					
					ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьПлановуюСумму(НоваяСтрока);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если ТипЗнч(СтрокаДокумент.Документ) = Тип("ДокументОбъект."+ВидыДокументов.ОПЗС) Тогда
				
				//Документы.ОтчетПроизводстваЗаСмену.ЗаполнитьСчетаУчетаВТабличнойЧасти(СтрокаДокумент.Документ, "Продукция");
				
				Если ЗаполнятьМатериалы Тогда
					
					Документы.ОтчетПроизводстваЗаСмену.ЗаполнитьМатериалыПоПродукцииУслугам(
						Объект.Материалы,
						Объект.Продукция.Выгрузить(),
						Объект.Услуги.Выгрузить(),
						Объект.Организация,
						Объект.Склад);
					
					Документы.ОтчетПроизводстваЗаСмену.ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, "Материалы");
					
				КонецЕсли;
				
			КонецЕсли;
			
			СтрокаДокумент.Документ.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;
	
	#КонецОбласти
	
	МассивДокументов = Новый Массив;
	
	Для Каждого СтрокаДокумент Из ТаблицаДокументов Цикл
		МассивДокументов.Добавить(СтрокаДокумент.Документ.Ссылка);
	КонецЦикла;
	
	Возврат МассивДокументов;
	
	//Возврат ТаблицаДокументов;
	
КонецФункции

Функция СформироватьКомментарий()
	
	Возврат "#Создан обработкой Выпуск и реализация продукции#";
	
КонецФункции

Функция СоздатьДокументОбъект(ИмяДокумента, ПараметрыЗаполненияОбщие = Неопределено, ПараметрыЗаполненияШапки = Неопределено)
	
	ДокументОбъект = Документы[ИмяДокумента].СоздатьДокумент();
	ЗаполнениеДокументов.Заполнить(ДокументОбъект);
	
	Если ЗначениеЗаполнено(ПараметрыЗаполненияОбщие) Тогда
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ПараметрыЗаполненияОбщие);
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ПараметрыЗаполненияОбщие.ПоДокументам[ИмяДокумента]);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗаполненияШапки) Тогда
		ЗаполнитьЗначенияСвойств(ДокументОбъект, ПараметрыЗаполненияШапки);
	КонецЕсли;
	
	Возврат ДокументОбъект;
	
КонецФункции

Функция ПолучитьСпецификации(Номенклатура)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СпецификацииНоменклатуры.Владелец КАК Номенклатура,
	               |	СпецификацииНоменклатуры.Ссылка КАК Спецификация,
	               |	ВЫБОР
	               |		КОГДА СпецификацииНоменклатуры.Владелец.ОсновнаяСпецификацияНоменклатуры = СпецификацииНоменклатуры.Ссылка
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Порядок
	               |ИЗ
	               |	Справочник.СпецификацииНоменклатуры КАК СпецификацииНоменклатуры
	               |ГДЕ
	               |	СпецификацииНоменклатуры.Владелец В(&Номенклатура)
	               |	И НЕ СпецификацииНоменклатуры.ПометкаУдаления
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Порядок
	               |ИТОГИ ПО
	               |	Номенклатура";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	
	Результат = Запрос.Выполнить();
	ВыборкаНоменклатура = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Спецификации = Новый Соответствие;
	Пока ВыборкаНоменклатура.Следующий() Цикл
		
		ВыборкаДетальная = ВыборкаНоменклатура.Выбрать();
		
		Пока ВыборкаДетальная.Следующий() Цикл
			Спецификации.Вставить(ВыборкаДетальная.Номенклатура, ВыборкаДетальная.Спецификация);
			Прервать;
		КонецЦикла;
	
	КонецЦикла;
	
	Возврат Спецификации;

КонецФункции

Функция ПолучитьДанныеЗаполнения()
	
	ОбщиеДанныеЗаполнения = Новый Структура("Дата, Организация, Склад, ТипЦен, ТипЦенПлановойСебестоимости");
	ЗаполнитьЗначенияСвойств(ОбщиеДанныеЗаполнения, ЭтотОбъект);
	ОбщиеДанныеЗаполнения.Вставить("ТипЦен", 		ТипЦенПродажи);
	ОбщиеДанныеЗаполнения.Вставить("Склад", 		Склад);
	ОбщиеДанныеЗаполнения.Вставить("Подразделение", Подразделение);
	
	СуммаВключаетНДС = Истина;
	Если ЗначениеЗаполнено(ОбщиеДанныеЗаполнения.ТипЦен) Тогда 
		СуммаВключаетНДС = ОбщиеДанныеЗаполнения.ТипЦен.ЦенаВключаетНДС;
	Иначе
		ОбщиеДанныеЗаполнения.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоПродажнымЦенам);
	КонецЕсли;
	ОбщиеДанныеЗаполнения.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
	
	Если Не ЗначениеЗаполнено(ОбщиеДанныеЗаполнения.ТипЦенПлановойСебестоимости) Тогда 
		ОбщиеДанныеЗаполнения.Вставить("СпособЗаполненияПлановойСебестоимости", Перечисления.СпособыЗаполненияЦен.ПоПлановымЦенам);
	КонецЕсли;
	
	ОбщиеДанныеЗаполнения.Вставить("Комментарий", 					СформироватьКомментарий());
	ОбщиеДанныеЗаполнения.Вставить("СоздаватьОтчетыПроизводства", 	Ложь);
	ОбщиеДанныеЗаполнения.Вставить("СоздаватьРеализации", 			Ложь);
	
	#Область ДанныеЗаполнения_ПоДокументам
	
	ОбщиеДанныеЗаполнения.Вставить("ПоДокументам", Новый Структура);
	
	ДанныеЗаполненияДокумента = Новый Структура;
	ДанныеЗаполненияДокумента.Вставить("Дата", НачалоДня(ОбщиеДанныеЗаполнения.Дата) + 1);
	ДанныеЗаполненияДокумента.Вставить("СчетЗатрат", ПланыСчетов.Хозрасчетный.ОсновноеПроизводство);
	Если ЗначениеЗаполнено(Подразделение) Тогда 
		ДанныеЗаполненияДокумента.Вставить("ПодразделениеОрганизации", 	Подразделение);
		ДанныеЗаполненияДокумента.Вставить("ПодразделениеЗатрат", 		Подразделение);
	КонецЕсли;
	
	ОбщиеДанныеЗаполнения.ПоДокументам.Вставить("ОтчетПроизводстваЗаСмену", ДанныеЗаполненияДокумента);
	
	ДанныеЗаполненияДокумента = Новый Структура;
	ДанныеЗаполненияДокумента.Вставить("Дата", 					КонецДня(ОбщиеДанныеЗаполнения.Дата));
	ДанныеЗаполненияДокумента.Вставить("ВидОперации", 			Перечисления.ВидыОперацийРеализацияТоваров.Товары);
	ДанныеЗаполненияДокумента.Вставить("СпособЗачетаАвансов", 	Перечисления.СпособыЗачетаАвансов.Автоматически);
	ДанныеЗаполненияДокумента.Вставить("ВалютаДокумента", 		ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	ДанныеЗаполненияДокумента.Вставить("КурсВзаиморасчетов", 	1);
	ДанныеЗаполненияДокумента.Вставить("КратностьВзаиморасчетов", 1);
	
	ОбщиеДанныеЗаполнения.ПоДокументам.Вставить("РеализацияТоваровУслуг", ДанныеЗаполненияДокумента);
	
	#КонецОбласти
	
	Возврат ОбщиеДанныеЗаполнения;

КонецФункции

#КонецОбласти

#Область ДанныеОТоварах

Функция ПолучитьДанныеОТоварах(ТабДокумент)

	ДеревоТоваров = ПолучитьОписаниеТаблицыТоваров();
	
	ТекНомерПоследнейСтроки 	= Мин(НомерПоследнейСтроки, 	ТабДокумент.ВысотаТаблицы);
	ТекНомерПоследнейКолонки 	= Мин(НомерПоследнейКолонки, 	ТабДокумент.ШиринаТаблицы);
	
	МассивНоменклатуры 	= Новый Массив;
	Контрагенты 		= Новый Массив;
	
	Для НомерСтроки = НомерПервойСтроки По ТекНомерПоследнейСтроки Цикл
		
		Получатель = ПолучитьЗначениеЯчейки(ТабДокумент, НомерСтроки, 1, НомерСтроки, 1);
		
		СтрокаПолучатель = Неопределено;
		Для НомерКолонки = НомерПервойКолонки По ТекНомерПоследнейКолонки Цикл
			
			Номенклатура 	= ПолучитьЗначениеЯчейки(ТабДокумент, 1, НомерКолонки, 1, НомерКолонки);
			Количество 		= ПолучитьЗначениеЯчейки(ТабДокумент, НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
			Количество 		= ?(ТипЗнч(Количество) = Тип("Число"), Количество, 0);
			
			Если Не ЗначениеЗаполнено(Номенклатура)
				Или ТипЗнч(Номенклатура) <> Тип("СправочникСсылка.Номенклатура")
				Или Количество <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если МассивНоменклатуры.Найти(Номенклатура) = Неопределено Тогда
				МассивНоменклатуры.Добавить(Номенклатура);
			КонецЕсли;
			
			Если СтрокаПолучатель = Неопределено Тогда
				
				СтрокаПолучатель 				= ДеревоТоваров.Строки.Добавить();
				СтрокаПолучатель.Контрагент 	= Получатель;
				СтрокаПолучатель 				= СтрокаПолучатель.Строки;
				
				Если ЗначениеЗаполнено(Получатель) И Контрагенты.Найти(Получатель) = Неопределено Тогда
					Контрагенты.Добавить(Получатель);
				КонецЕсли;
				
			КонецЕсли;
			
			НоваяСтрока = СтрокаПолучатель.Добавить();
			НоваяСтрока.Номенклатура 		= Номенклатура;
			НоваяСтрока.Количество 			= Количество;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ДанныеОТоварах = Новый Структура;
	ДанныеОТоварах.Вставить("ДеревоНоменклатуры", 	ДеревоТоваров);
	ДанныеОТоварах.Вставить("Номенклатура", 		МассивНоменклатуры);
	ДанныеОТоварах.Вставить("Контрагенты", 			Контрагенты);
	
	Возврат ДанныеОТоварах;

КонецФункции

Функция ПолучитьТаблицуИзДереваТоваров(ДеревоТоваров)
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	Для Каждого Колонка Из ДеревоТоваров.Колонки Цикл
		ТаблицаТоваров.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения, Колонка.Заголовок);
	КонецЦикла;
	
	//ТаблицаТоваров = ПолучитьОписаниеТаблицыТоваров(Ложь);
	Для Каждого СтрокаПолучатель Из ДеревоТоваров.Строки Цикл
		Для Каждого СтрокаТовары Из СтрокаПолучатель.Строки Цикл
			НоваяСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовары);
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаТоваров;

КонецФункции

Функция ПолучитьДоговорыСРеквизитами(Контрагенты, Организация)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	               |	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента,
	               |	ДоговорыКонтрагентов.ВалютаВзаиморасчетов КАК ВалютаДокумента,
	               |	ДоговорыКонтрагентов.ТипЦен КАК ТипЦен,
	               |	1 КАК Порядок
	               |ПОМЕСТИТЬ ВТДоговоры
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	НЕ ДоговорыКонтрагентов.ПометкаУдаления
	               |	И ДоговорыКонтрагентов.Организация = &Организация
	               |	И ДоговорыКонтрагентов.ВидДоговора = &ВидДоговора
	               |	И ДоговорыКонтрагентов.Владелец В(&Контрагенты)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ОсновныеДоговоры.Контрагент КАК Контрагент,
	               |	ОсновныеДоговоры.Договор КАК ДоговорКонтрагента
	               |ПОМЕСТИТЬ ВТДоговорыРегистр
	               |ИЗ
	               |	РегистрСведений.ОсновныеДоговорыКонтрагента КАК ОсновныеДоговоры
	               |ГДЕ
	               |	ОсновныеДоговоры.ВидДоговора = &ВидДоговора
	               |	И ОсновныеДоговоры.Организация = &Организация
	               |	И ОсновныеДоговоры.Контрагент В(&Контрагенты)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТДоговоры.Контрагент КАК Контрагент,
	               |	ВТДоговоры.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |	ВТДоговоры.ВалютаДокумента КАК ВалютаДокумента,
	               |	ВТДоговоры.ТипЦен КАК ТипЦен,
	               |	ВЫБОР
	               |		КОГДА ВТДоговорыРегистр.ДоговорКонтрагента ЕСТЬ NULL
	               |			ТОГДА ВТДоговоры.Порядок
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Порядок
	               |ИЗ
	               |	ВТДоговоры КАК ВТДоговоры
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоговорыРегистр КАК ВТДоговорыРегистр
	               |		ПО ВТДоговоры.Контрагент = ВТДоговорыРегистр.Контрагент
	               |			И ВТДоговоры.ДоговорКонтрагента = ВТДоговорыРегистр.ДоговорКонтрагента
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Контрагент,
	               |	Порядок";
	
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.СПокупателем);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция ПолучитьДоговор(Договоры, Контрагент)

	Если Договоры = Неопределено Тогда 
		Возврат Неопределено;
	КонецЕсли;
	
	НайденнныеДоговоры = Договоры.НайтиСтроки(Новый Структура("Контрагент", Контрагент));
	Если НайденнныеДоговоры.Количество() > 0 Тогда
		Возврат НайденнныеДоговоры[0].ДоговорКонтрагента;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

Функция ПолучитьЗначениеЯчейки(ТабДокумент, НомерПервСтроки, НомерПервКолонки, НомерПоследСтроки, НомерПоследКолонки, Значение = Истина)
	
	Попытка
		Возврат ТабДокумент.Область(НомерПервСтроки, НомерПервКолонки, НомерПоследСтроки, НомерПоследКолонки)[?(Значение, "Значение", "Текст")];
	Исключение
		Возврат Неопределено
	КонецПопытки;
	
КонецФункции

Функция ПолучитьОписаниеТаблицыТоваров(Дерево = Истина)
	
	Число15_2 = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой));
	Число15_3 = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Любой));
	
	ТаблицаСведений = ?(Дерево, Новый ДеревоЗначений, Новый ТаблицаЗначений);
	ТаблицаСведений.Колонки.Добавить("Контрагент");
	ТаблицаСведений.Колонки.Добавить("Номенклатура", 		Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаСведений.Колонки.Добавить("Количество", 			Число15_3);
	
	Возврат ТаблицаСведений;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВерсияОбъектаСтрокой() Экспорт
	
	Возврат "0.9";
	
КонецФункции

#КонецОбласти