#Область ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	//ОбработкаОбъект.ЗагрузитьНастройкиВКомпоновщикНастроек(Объект.КомпоновщикНастроек);
	ОбработкаОбъект.ЗагрузитьНастройкиВКомпоновщикНастроек(УникальныйИдентификатор);
	ОбработкаОбъект.НастроитьПолеТабличногоДокумента(НоменлатураКонтрагентыТД);
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Дата = ТекущаяДата();
	
	Объект.ОтображаемаяПанель = Элементы.ОтображаемаяПанель.СписокВыбора[0].Значение;
	УстановитьПанель(ЭтаФорма);
	
	АдресПубликации = "https://infostart.ru/public/1006122/";
	//Элементы.НадписьПубликация.Заголовок = Новый ФорматированнаяСтрока("Публикация на Инфостарт: ", Новый ФорматированнаяСтрока(АдресПубликации,,,, АдресПубликации));
	Элементы.НадписьПубликация.Заголовок = Новый ФорматированнаяСтрока("Публикация на ", Новый ФорматированнаяСтрока("INFOSTART.RU",,,, АдресПубликации));
	
	ДоступныеДокументы.Добавить("ОтчетПроизводстваЗаСмену", "Отчет производства за смену");
	ДоступныеДокументы.Добавить("РеализацияТоваровУслуг", "Реализация (акты, накладные)");
	
	ПараметрыДоступныхДокументов = Новый Структура;
	
	Для Каждого ДоступныйДокумент Из ДоступныеДокументы Цикл
	КонецЦикла;
	
	ЭлементыКоманднойПанели = Элементы.СписокДокументов.КоманднаяПанель.ПодчиненныеЭлементы.СписокДокументовСоздать.ПодчиненныеЭлементы;
	ЭлементыКонтекстногоМеню = Элементы.СписокДокументов.КонтекстноеМеню.ПодчиненныеЭлементы.СписокДокументовКонтекстноеМенюСоздать.ПодчиненныеЭлементы;
	
	УстановитьДоступныеКоманды(ЭлементыКоманднойПанели);
	УстановитьДоступныеКоманды(ЭлементыКонтекстногоМеню);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступныеКоманды(СписокЭлементов)
	
	Для Каждого Элемент Из СписокЭлементов Цикл
		
		ИмяКоманды = СтрЗаменить(Элемент.Имя, 	"СписокДокументовСоздатьПоПараметру", "");
		ИмяКоманды = СтрЗаменить(ИмяКоманды, 	"СписокДокументовКонтекстноеМенюСоздатьПоПараметру", "");
		
		Если ДоступныеДокументы.НайтиПоЗначению(ИмяКоманды) = Неопределено Тогда
			Элемент.Доступность = Ложь;
			Элемент.Видимость = Ложь;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УстановитьПанель(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображаемаяПанельПриИзменении(Элемент)
	
	УстановитьПанель(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(ТЧ, Флаг)
	
	Для Каждого СтрокаТЧ Из ТЧ Цикл
		СтрокаТЧ.Пометка = Флаг;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиСнять(Команда)
	УстановитьФлажки(Объект.СозданныеДокументы, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиУстановить(Команда)
	УстановитьФлажки(Объект.СозданныеДокументы, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуруИКонтрагентов(Команда)
	ЗаполнитьНоменклатуруИКонтрагентовНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруИКонтрагентовНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ЭлементыСправочников = ОбработкаОбъект.ПолучитьСправочникиПоОтбору(Объект.КомпоновщикНастроек.ПолучитьНастройки());
	
	ЗаполнитьСправочники(ЭтаФорма, ЭлементыСправочников.Номенклатура);
	ЗаполнитьСправочники(ЭтаФорма, ЭлементыСправочников.Контрагенты, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначениеЯчейки(ТабДокумент, НомерПервСтроки, НомерПервКолонки, НомерПоследСтроки, НомерПоследКолонки, Значение = Истина)
	
	Попытка
		Возврат ТабДокумент.Область(НомерПервСтроки, НомерПервКолонки, НомерПоследСтроки, НомерПоследКолонки)[?(Значение, "Значение", "Текст")];
	Исключение
		Возврат Неопределено
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьИтоги(Область = Неопределено) Экспорт
	
	ТабДокумент = НоменлатураКонтрагентыТД;
	
	ТекНомерПервойКолонки 		= Объект.НомерПервойКолонки;
	ТекНомерПоследнейКолонки 	= Мин(Объект.НомерПоследнейКолонки, ТабДокумент.ШиринаТаблицы);
	ТекНомерПоследнейСтроки 	= Мин(Объект.НомерПоследнейСтроки, ТабДокумент.ВысотаТаблицы);


	Если ТипЗнч(Область) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		Если Не (Область.Лево >= Объект.НомерПервойКолонки
			   И Область.Лево <= ТекНомерПоследнейКолонки
			   И Область.Верх >= Объект.НомерПервойСтроки
			   И Область.Верх <= ТекНомерПоследнейСтроки) Тогда
			   Возврат;
		КонецЕсли;
		
		ТекНомерПервойКолонки 			= Область.Лево;
		ТекНомерПоследнейКолонки 		= Область.Лево;
		   
	КонецЕсли;
	
	Для НомерКолонки = ТекНомерПервойКолонки По ТекНомерПоследнейКолонки Цикл
		
		Количество = 0;
		
		Для НомерСтроки = Объект.НомерПервойСтроки По ТекНомерПоследнейСтроки Цикл
			
			//ТекКоличество = ТабДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).Значение;
			ТекКоличество = ПолучитьЗначениеЯчейки(ТабДокумент, НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
			ТекКоличество = ?(ТипЗнч(ТекКоличество) = Тип("Число"), ТекКоличество, 0);
			
			Количество = Количество + ТекКоличество;
			
		КонецЦикла;
		
		ЯчейкаИтого = ТабДокумент.Область(Объект.НомерСтрокиИтого, НомерКолонки, Объект.НомерСтрокиИтого, НомерКолонки);
		Попытка
			ЯчейкаИтого.Значение = Количество;
		Исключение
		КонецПопытки;
		
		
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПанель(Форма)
	
	Элементы = Форма.Элементы;
	Элементы.Страницы.ТекущаяСтраница = Элементы["Страница" + Форма.Объект.ОтображаемаяПанель];
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ЗАПОЛНЕНИЕ ТАБЛИЧНОГО ДОКУМЕНТА

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСправочники(Форма, МассивЭлементов, ЗаполнятьКонтрагентов = Ложь) Экспорт
	
	ТабДокумент = Форма.НоменлатураКонтрагентыТД;
	
	Объект = Форма.Объект;
	
	Если ЗаполнятьКонтрагентов Тогда
		НомерПоследнейЯчейки 	= Мин(Объект.НомерПоследнейСтроки, ТабДокумент.ВысотаТаблицы);
		НомерПервойЯчейки 		= Объект.НомерПервойСтроки;
	Иначе
		НомерПоследнейЯчейки 	= Мин(Объект.НомерПоследнейКолонки, ТабДокумент.ШиринаТаблицы);
		НомерПервойЯчейки 		= Объект.НомерПервойКолонки;
	КонецЕсли;
	
	ИндексВМассиве = 0;
	
	Для НомерЯчейки = НомерПервойЯчейки По НомерПоследнейЯчейки Цикл
		
		Попытка
			
			Если ЗаполнятьКонтрагентов Тогда
				НомерСтроки 	= НомерЯчейки;
				НомерКолонки 	= 1;
			Иначе
				НомерСтроки 	= 1;
				НомерКолонки 	= НомерЯчейки;
			КонецЕсли;
			
			Область = ТабДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
			
		Исключение
			Прервать;
		КонецПопытки;
		
		Область.Значение = ?(ТипЗнч(МассивЭлементов) = Тип("Массив") И (ИндексВМассиве + 1 <= МассивЭлементов.Количество()),
			МассивЭлементов[ИндексВМассиве],
				Область.Значение = "");
		
		ИндексВМассиве = ИндексВМассиве + 1;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура НоменлатураКонтрагентыТДВыбор(Элемент, Область, СтандартнаяОбработка)
	
	//Если Область.СодержитЗначение
	//	И (Область.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура"))
	//		Или Область.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Контрагенты"))) Тогда
	//	//Область.ЭлементУправления.КнопкаОткрытия = Истина;
	//	//Область.ЭлементУправления.КнопкаВыбора = Истина;
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменлатураКонтрагентыТДПриИзмененииСодержимогоОбласти(Элемент, Область)

	ПересчитатьИтоги(Область);
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СОЗДАНИЕ ДОКУМЕНТОВ

&НаКлиенте
Процедура СоздатьОтчетПроизводствоЗаСмену(Команда)
	
	СформироватьДокументыНаСервере(Новый Структура("СоздаватьОтчетыПроизводства", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРеализацияТоваровУслуг(Команда)
	
	СформироватьДокументыНаСервере(Новый Структура("СоздаватьРеализации", Истина));
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДокументыНаСервере(РежимФормирования)

	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	МассивДокументов = ОбработкаОбъект.СоздатьДокументы(НоменлатураКонтрагентыТД, РежимФормирования);
	
	Если ЗначениеЗаполнено(МассивДокументов) Тогда
		
		Для Каждого ДокументСсылка Из МассивДокументов Цикл
			НоваяСтрока = Объект.СозданныеДокументы.Добавить();
			НоваяСтрока.Документ = ДокументСсылка;
			НоваяСтрока.Пометка = Истина;
		КонецЦикла;
		
		Объект.ОтображаемаяПанель = Элементы.ОтображаемаяПанель.СписокВыбора[1].Значение;
		УстановитьПанель(ЭтаФорма);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Параметр = Неопределено;
	
	Если Отказ Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВыборИзМенюЗавершение", ЭтаФорма, Неопределено);
		ПоказатьВыборИзМеню(ОписаниеОповещения, ДоступныеДокументы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборИзМенюЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	//ПараметрыФормы.Вставить("ВидОперации", ПредопределенноеЗначение("Перечисление.ВидыОперацийРеализацияТоваров.Товары"));
	ОткрытьФорму("Документ."+ВыбранныйЭлемент.Значение+".Форма.ФормаДокумента", ПараметрыФормы, Элементы.СписокДокументов);
	//ОткрытьФорму("Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента", ПараметрыФормы, Элементы.СписокДокументов);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(ВидДокумента)

	

КонецПроцедуры



#КонецОбласти
