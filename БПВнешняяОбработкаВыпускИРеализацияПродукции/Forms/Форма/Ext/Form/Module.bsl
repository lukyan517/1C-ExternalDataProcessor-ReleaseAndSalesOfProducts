#Область ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьНастройкиВКомпоновщикНастроек(УникальныйИдентификатор);
	ОбработкаОбъект.НастроитьПолеТабличногоДокумента(НоменлатураКонтрагентыТД);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокДокументов,
		"Комментарий",
		ОбработкаОбъект.ПолучитьКомментарий(),
		ВидСравненияКомпоновкиДанных.Содержит,,,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
		
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Дата = ТекущаяДата();
	
	Объект.ОтображаемаяПанель = Элементы.ОтображаемаяПанель.СписокВыбора[0].Значение;
	УстановитьПанель(ЭтаФорма);
	
	АдресПубликации = "https://infostart.ru/public/1609388/";
	Элементы.НадписьПубликация.Заголовок = Новый ФорматированнаяСтрока(Новый ФорматированнаяСтрока("Публикация на INFOSTART.RU",,,, АдресПубликации));
	
	ДоступныеДокументы = ОбработкаОбъект.ПолучитьДоступныеДокументы();
	
	//ПараметрыДоступныхДокументов = Новый Структура;
	//Для Каждого ДоступныйДокумент Из ДоступныеДокументы Цикл
	//КонецЦикла;
	
	ЭлементыКоманднойПанели = Элементы.СписокДокументов.КоманднаяПанель.ПодчиненныеЭлементы.СписокДокументовСоздать.ПодчиненныеЭлементы;
	ЭлементыКонтекстногоМеню = Элементы.СписокДокументов.КонтекстноеМеню.ПодчиненныеЭлементы.СписокДокументовКонтекстноеМенюСоздать.ПодчиненныеЭлементы;
	
	УстановитьДоступныеКоманды(ЭлементыКоманднойПанели);
	УстановитьДоступныеКоманды(ЭлементыКонтекстногоМеню);
	
	ЗаголовокФормы = НСтр("ru = 'Обработка Выпуск и реализация продукции для БП 3.0 (%1)'");
	ЗаголовокФормы = СтрШаблон(ЗаголовокФормы, ВерсияОбъектаСтрокойНаСервере());
	Заголовок = ЗаголовокФормы;
	
	// Доступ к настройкам реализован через всплывающую группу.
	Элементы.ОтображаемаяПанель.СписокВыбора.Удалить(2);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступныеКоманды(СписокЭлементов)
	
	Для Каждого Элемент Из СписокЭлементов Цикл
		
		ИмяКоманды = СтрЗаменить(Элемент.Имя, 	"СписокДокументовСоздатьПоПараметру", "");
		ИмяКоманды = СтрЗаменить(ИмяКоманды, 	"СписокДокументовКонтекстноеМенюСоздатьПоПараметру", "");
		
		Если ДоступныеДокументы.НайтиПоЗначению(ИмяКоманды) = Неопределено Тогда
			Элемент.Доступность = Ложь;
			Элемент.Видимость = Ложь;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	УстановитьПанель(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтображаемаяПанельПриИзменении(Элемент)
	
	УстановитьПанель(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(ТЧ, Флаг)
	
	Для Каждого СтрокаТЧ Из ТЧ Цикл
		СтрокаТЧ.Пометка = Флаг;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиСнять(Команда)
	УстановитьФлажки(Объект.СозданныеДокументы, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ФлажкиУстановить(Команда)
	УстановитьФлажки(Объект.СозданныеДокументы, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатуруИКонтрагентов(Команда)
	ЗаполнитьНоменклатуруИКонтрагентовНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатуруИКонтрагентовНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ЭлементыСправочников = ОбработкаОбъект.ПолучитьСправочникиПоОтбору(Объект.КомпоновщикНастроек.ПолучитьНастройки());
	
	ЗаполнитьСправочники(ЭтаФорма, ЭлементыСправочников.Номенклатура);
	ЗаполнитьСправочники(ЭтаФорма, ЭлементыСправочников.Контрагенты, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщегоНазначения

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначениеЯчейки(ТабДокумент, НомерПервСтроки, НомерПервКолонки, НомерПоследСтроки, НомерПоследКолонки, Значение = Истина)
	
	Попытка
		Возврат ТабДокумент.Область(НомерПервСтроки, НомерПервКолонки, НомерПоследСтроки, НомерПоследКолонки)[?(Значение, "Значение", "Текст")];
	Исключение
		Возврат Неопределено
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Процедура ПересчитатьИтоги(Область = Неопределено) Экспорт
	
	ТабДокумент = НоменлатураКонтрагентыТД;
	
	ТекНомерПервойКолонки 		= Объект.НомерПервойКолонки;
	ТекНомерПоследнейКолонки 	= Мин(Объект.НомерПоследнейКолонки, ТабДокумент.ШиринаТаблицы);
	ТекНомерПоследнейСтроки 	= Мин(Объект.НомерПоследнейСтроки, ТабДокумент.ВысотаТаблицы);


	Если ТипЗнч(Область) = Тип("ОбластьЯчеекТабличногоДокумента") Тогда
		
		Если Не (Область.Лево >= Объект.НомерПервойКолонки
			   И Область.Лево <= ТекНомерПоследнейКолонки
			   И Область.Верх >= Объект.НомерПервойСтроки
			   И Область.Верх <= ТекНомерПоследнейСтроки) Тогда
			   Возврат;
		КонецЕсли;
		
		ТекНомерПервойКолонки 			= Область.Лево;
		ТекНомерПоследнейКолонки 		= Область.Лево;
		   
	КонецЕсли;
	
	Для НомерКолонки = ТекНомерПервойКолонки По ТекНомерПоследнейКолонки Цикл
		
		Количество = 0;
		
		Для НомерСтроки = Объект.НомерПервойСтроки По ТекНомерПоследнейСтроки Цикл
			
			//ТекКоличество = ТабДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки).Значение;
			ТекКоличество = ПолучитьЗначениеЯчейки(ТабДокумент, НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
			ТекКоличество = ?(ТипЗнч(ТекКоличество) = Тип("Число"), ТекКоличество, 0);
			
			Количество = Количество + ТекКоличество;
			
		КонецЦикла;
		
		ЯчейкаИтого = ТабДокумент.Область(Объект.НомерСтрокиИтого, НомерКолонки, Объект.НомерСтрокиИтого, НомерКолонки);
		Попытка
			ЯчейкаИтого.Значение = Количество;
		Исключение
		КонецПопытки;
		
		
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПанель(Форма)

	Элементы = Форма.Элементы;
	//Форма.Объект.ОтображаемаяПанель = Элементы.ОтображаемаяПанель.СписокВыбора[0].Значение; // Отображаем только первую страницу с табличным документом.
	Элементы.Страницы.ТекущаяСтраница = Элементы["Страница" + Форма.Объект.ОтображаемаяПанель];
	
КонецПроцедуры

&НаСервере
Функция ВерсияОбъектаСтрокойНаСервере()
	
	Возврат РеквизитФормыВЗначение("Объект").ВерсияОбъектаСтрокой();
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеТабличногоДокумента

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСправочники(Форма, МассивЭлементов, ЗаполнятьКонтрагентов = Ложь) Экспорт
	
	ТабДокумент = Форма.НоменлатураКонтрагентыТД;
	
	Объект = Форма.Объект;
	
	Если ЗаполнятьКонтрагентов Тогда
		НомерПоследнейЯчейки 	= Мин(Объект.НомерПоследнейСтроки, ТабДокумент.ВысотаТаблицы);
		НомерПервойЯчейки 		= Объект.НомерПервойСтроки;
	Иначе
		НомерПоследнейЯчейки 	= Мин(Объект.НомерПоследнейКолонки, ТабДокумент.ШиринаТаблицы);
		НомерПервойЯчейки 		= Объект.НомерПервойКолонки;
	КонецЕсли;
	
	ИндексВМассиве = 0;
	
	Для НомерЯчейки = НомерПервойЯчейки По НомерПоследнейЯчейки Цикл
		
		Попытка
			
			Если ЗаполнятьКонтрагентов Тогда
				НомерСтроки 	= НомерЯчейки;
				НомерКолонки 	= 1;
			Иначе
				НомерСтроки 	= 1;
				НомерКолонки 	= НомерЯчейки;
			КонецЕсли;
			
			Область = ТабДокумент.Область(НомерСтроки, НомерКолонки, НомерСтроки, НомерКолонки);
			
		Исключение
			Прервать;
		КонецПопытки;
		
		Область.Значение = ?(ТипЗнч(МассивЭлементов) = Тип("Массив") И (ИндексВМассиве + 1 <= МассивЭлементов.Количество()),
			МассивЭлементов[ИндексВМассиве],
				Область.Значение = "");
		
		ИндексВМассиве = ИндексВМассиве + 1;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура НоменлатураКонтрагентыТДВыбор(Элемент, Область, СтандартнаяОбработка)
	
	//Если Область.СодержитЗначение
	//	И (Область.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура"))
	//		Или Область.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Контрагенты"))) Тогда
	//	//Область.ЭлементУправления.КнопкаОткрытия = Истина;
	//	//Область.ЭлементУправления.КнопкаВыбора = Истина;
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменлатураКонтрагентыТДПриИзмененииСодержимогоОбласти(Элемент, Область)

	ПересчитатьИтоги(Область);
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеДокументов

&НаКлиенте
Процедура СоздатьОтчетПроизводствоЗаСмену(Команда)
	
	СформироватьДокументыНаСервере(ДоступныеДокументы[0].Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьРеализацияТоваровУслуг(Команда)
	
	СформироватьДокументыНаСервере(ДоступныеДокументы[1].Значение);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДокументыНаСервере(ВидыДокументов, ДополнительныеПараметрыДокументов = Неопределено)

	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	МассивДокументов = ОбработкаОбъект.СоздатьДокументы(НоменлатураКонтрагентыТД, ВидыДокументов, ДополнительныеПараметрыДокументов);
	
	Если ЗначениеЗаполнено(МассивДокументов) Тогда
		
		Для Каждого ДокументСсылка Из МассивДокументов Цикл
			НоваяСтрока = Объект.СозданныеДокументы.Добавить();
			НоваяСтрока.Документ = ДокументСсылка;
			НоваяСтрока.Пометка = Истина;
		КонецЦикла;
		
		Объект.ОтображаемаяПанель = Элементы.ОтображаемаяПанель.СписокВыбора[1].Значение;
		УстановитьПанель(ЭтаФорма);
		
		Элементы.ГруппаДокументы.Показать();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Параметр = Неопределено;
	
	Если Отказ Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВыборИзМенюЗавершение", ЭтаФорма, Неопределено);
		ПоказатьВыборИзМеню(ОписаниеОповещения, ДоступныеДокументы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыборИзМенюЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ОткрытьФорму(СтрШаблон("Документ.%1.Форма.ФормаДокумента", ВыбранныйЭлемент.Значение), ПараметрыФормы, Элементы.СписокДокументов);

КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокумент(ВидДокумента)

	

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область НаУдаление

&НаКлиентеНаСервереБезКонтекста
Процедура Удалить_УстановитьЗаголовокГруппыНастройки(Форма)
	
	Возврат;
	
	Объект = Форма.Объект;
	
	Настройки = Новый Массив;
	Настройки.Добавить(?(ЗначениеЗаполнено(Объект.НоменклатурнаяГруппа), Объект.НоменклатурнаяГруппа, "Без НГ"));
	Настройки.Добавить(?(ЗначениеЗаполнено(Объект.Подразделение), Объект.Подразделение, "Без подразделения"));
	Настройки.Добавить(?(ЗначениеЗаполнено(Объект.Склад), Объект.Склад, "Без склада"));
	
	Форма.Элементы.ГруппаНастройки_.Заголовок = "Настройки: " + СтрСоединить(Настройки, ", ");
	
КонецПроцедуры //}}ОЦРВ, Лукьяненко С. Ю., stanislav.lukyanenko@ocrv.ru, ТП 00-01.01.01.00_01, 10.11.2020, окончание

#КонецОбласти